buildscript {
    ext.kotlin_version = '1.0.0-beta-1038'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.salomonbrys.kotson"
version = '1.5.0'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

repositories {
    mavenCentral()

    maven {
        url 'http://repository.jetbrains.com/repo'

    }
}

dependencies {
    compile "com.google.code.gson:gson:2.4"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'org.jetbrains.spek:spek:0.1.180'
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Kotson'
                    packaging 'jar'
                    artifactId 'kotson'
                    description 'Kotlin bindings for JSON manipulation via Gson'
                    url 'https://github.com/SalomonBrys/Kotson'

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'salomonbrys'
                            name 'Salomon BRYS'
                            email 'salomon.brys@gmail.com'
                        }
                    }

                    scm {
                        url 'https://github.com/SalomonBrys/Kotson'
                        connection 'scm:git:https://github.com/SalomonBrys/Kotson.git'
                        developerConnection 'scm:git:git@github.com:SalomonBrys/Kotson.git'
                        tag 'HEAD'
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/SalomonBrys/Kotson/issues'
                    }
                }
            }
        }
    }
}
